apply plugin: 'com.android.application'

ext {
    versionMajor = 0
    versionMinor = 1
    versionPatch = 0
    versionBuild = 0
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "pe.utec.cs.androidcourse"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

private Integer generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 1000 + ext.versionPatch * 100 + ext.versionBuild
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

ext {
    supportVersion = '27.1.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // -- Android --
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // -- Logging --
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // -- Testing --
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
